plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.dokka'
    id 'maven-publish'
}

android {
    namespace 'knf.hydra.tools.core.ktx'
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    dokkaPlugin 'org.jetbrains.dokka:android-documentation-plugin:1.8.10'
}

tasks.dokkaHtml.configure {
    outputDirectory.set(file("$rootDir/libs/core/core-ktx/docs"))
    dokkaSourceSets {
        named("main") {
            includes.from("README.md")
            noAndroidSdkLink.set(false)
            sourceLink {
                remoteUrl.set(java.net.URL("https://github.com/hydra-app/tools/tree/main/libs/core/core-ktx/src/main/java"))
                remoteLineSuffix.set("#L")
            }
        }
    }
}

task dokkaJavadocJar(type: Jar) {
    dependsOn(tasks.dokkaJavadoc)
    def directories = new ArrayList<Object>()
    tasks.dokkaJavadoc.each {
        directories.add(it.outputDirectory)
    }
    from(directories)
    archiveClassifier.set("javadoc")
}

task dokkaHtmlJar(type: Jar) {
    dependsOn(tasks.dokkaHtml)
    def directories = new ArrayList<Object>()
    tasks.dokkaHtml.each {
        directories.add(it.outputDirectory)
    }
    from(directories)
    archiveClassifier.set("html-doc")
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives dokkaJavadocJar
    archives dokkaHtmlJar
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                url = uri("$buildDir/repository")
            }
        }
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'knf.hydra.tools'
                artifactId = 'core-ktx'
                version = rootProject.lib_version
                artifact(androidSourcesJar)
                artifact(dokkaJavadocJar)
                artifact(dokkaHtmlJar)
            }
        }
    }
}